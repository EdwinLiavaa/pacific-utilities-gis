'***********************************************************
'*       DaysToFillTanksB              Wolf 13.11.07       *
'*   Modified: Edwin 28.09.08                              *
'*   This program                                          *
'*           - imports the TMP_RoofDetails from Access     *
'*           - links Roof_MI to TMP_RoofDetails            *
'*           - adds the linked tables to mapwindow         *
'*           - creates a thematic map from the linked TAB  *
'***********************************************************
Include "MapBasic.DEF"
Include "Menu.DEF"

	Declare Sub DaysToFillTanksB
		
	Global PathVectorL As String
	Global PathBackdrop As String
	Global PathAccess As String
	Global DatabaseName As String

	Sub DaysToFillTanksB

	OnError Goto Err_Manage

	Dim MapWinID As Integer

	Close All
 
	Open Table PathVectorL + "Roof_MI.TAB" As Roof_MI

	Map From Roof_MI Max

	MapWinID = FrontWindow()

	Open Table PathBackdrop + "nauru050515.TAB" Interactive
	
	Add Map Auto Layer nauru050515

	Set Map Window MapWinID Zoom 1.0 Units "km" Center (166.9354, -0.504205)

'	++++++++++++++++ import Access Table +++++++++++++++++++++++++++++++++++++++++

	Register Table PathAccess + DatabaseName   
	Type ACCESS Table "TMP_RoofDetails" 
	Into PathVectorL + "TMP_RoofDetails.TAB"

	Open Table PathVectorL + "TMP_RoofDetails.TAB" Interactive

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++ Link MapInfo and Access Table +++++++++++++++++++++++++++++++

	Select * from Roof_MI, TMP_RoofDetails 
	where Roof_MI.Roof_ID = TMP_RoofDetails.Roof_ID 
	into LinkTable

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Add to current mapper +++++++++++++++++++++++++++++++

	set map redraw off
	Add Map Layer LinkTable
	set map redraw on

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Create thematic Map +++++++++++++++++++++++++++++++
  
	shade window MapWinID LinkTable with TMP_FillDaysBrutto ranges apply all use color Brush (2,12632256,16777215)  
		0: 0 Brush (2,12632256,16777215) Pen (1,2,0) ,
		0: 5 Brush (2,65280,16777215) Pen (1,2,0) ,
		5: 10 Brush (2,11796288,16777215) Pen (1,2,0) ,
		10: 20 Brush (2,16776960,16777215) Pen (1,2,0) ,
		20: 500 Brush (2,16711680,16777215) Pen (1,2,0) 
		default Brush (2,16777215,16777215) Pen (1,2,0)  
 
'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Create Map Legend +++++++++++++++++++++++++++++++
 
  set legend window MapWinID layer prev 
		display on 
		shades on 
		symbols off 
		lines off 
		count on 
		title "Days to fill Tanks this Month" Font ("Arial",1,12,0) 
		subtitle "no consumption calculated" Font ("Arial",0,8,0) 
		ascending off ranges Font ("Arial",0,8,0) 
		auto display off ,
		"No filling" display on ,
		auto display on ,
		auto display on ,
		auto display on ,
		auto display on  

	Set Window WIN_LEGEND 
		Position (0.5,3) Units "cm"
		Width 7 Units "cm"
		Height 4 Units "cm"

	Open Window Legend

	Run Menu Command M_QUERY_UNSELECT

	set map redraw off
	Set Map Layer LinkTable Label Font ("Arial",257,12,0,16777215) Overlap On
	Set Map Layer LinkTable Label Auto On 
   	Set Map Layer Roof_MI Display Global Global Pen (2,2,0)  Global Brush (6,16711680,16777215)
	set map redraw on

		Res_Main:
			Exit Sub

		Err_Manage:
			note "Error: " + str$(Err()) + ", " + Error$()
			Resume Res_Main

	End sub