'***********************************************************
'*       MapRoofConnect          Wolf    29.10.07          *
'*   Modified: Edwin 28.09.08                              *
'*   This program                                          *
'*           - imports the TMP_RoofTank from Access        *
'*           - links Roof_MI to TMP_RoofTank               *
'*           - adds the linked tables to mapwindow         *
'*           - creates a thematic map from the linked TAB  *
'***********************************************************
Include "MapBasic.DEF"
Include "Menu.DEF"

	Declare Sub MapRoofConnect
				
	Global PathVectorL As String
	Global PathBackdrop As String
	Global PathAccess As String
	Global DatabaseName As String

	Sub MapRoofConnect

	OnError Goto Err_Manage

	Dim MapWinID As Integer

	Close All 
	
	Open Table PathVectorL + "Roof_MI.TAB" As Roof_MI

	Map From Roof_MI Max

	MapWinID = FrontWindow()

	Open Table PathBackdrop + "nauru050515.TAB" Interactive
	
	Add Map Auto Layer nauru050515

	Set Map Window MapWinID Zoom 1.0 Units "km" Center (166.9354, -0.504205)

'	++++++++++++++++ import Access Table +++++++++++++++++++++++++++++++++++++++++

	Register Table PathAccess + DatabaseName  
	Type ACCESS Table "TMP_RoofTank" 
	Into PathVectorL + "TMP_RoofTank.TAB"

	Open Table PathVectorL + "TMP_RoofTank.TAB" Interactive

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++ Link MapInfo and Access Table +++++++++++++++++++++++++++++++

	Select * from Roof_MI, TMP_RoofTank 
	where Roof_MI.Roof_ID = TMP_RoofTank.Roof_ID 
	into LinkTable

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Add to current mapper +++++++++++++++++++++++++++++++

	set map redraw off
	Add Map Layer LinkTable 
	set map redraw on

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Create thematic Map +++++++++++++++++++++++++++++++

	shade window MapWinID LinkTable with TankConnected values  
	"NO" Brush (2,12632256,16777215) Pen (1,2,0) ,
	"" Brush (2,16711680,16777215) Pen (1,2,0) ,
	"YES" Brush (2,65280,16777215) Pen (1,2,0) 
	default Brush (1,0,16777215) Pen (1,2,0) 

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Create Map Legend +++++++++++++++++++++++++++++++

	set legend window MapWinID layer prev 
	display on 
	shades on 
	symbols off 
	lines off 
	count on 
	title "Roofs with Tanks" Font ("Arial",0,9,0) 
	subtitle auto Font ("Arial",0,8,0) 
	ascending on ranges Font ("Arial",0,8,0) 
	auto display off ,
	"Tank Not Connected" display on ,
	"Unknown" display on ,
	"Tank Connected" display on 
	

	Set Window WIN_LEGEND 
		Position (0.5,3) Units "cm"
		Width 5.5 Units "cm"
		Height 3 Units "cm"

	Open Window Legend

	Run Menu Command M_QUERY_UNSELECT

		Res_Main:
			Exit Sub

		Err_Manage:
			note "Error: " + str$(Err()) + ", " + Error$()
			Resume Res_Main

	End sub