'***********************************************************
'*       MapWasteDisposal          Edwin 27.05.09          *
'*   Modified: Edwin 28.09.08                              *
'*   This program                                          *
'*           - imports the TMP_RoofTank from Access        *
'*           - links Roof_MI to TMP_RoofTank               *
'*           - adds the linked tables to mapwindow         *
'*           - creates a thematic map from the linked TAB  *
'***********************************************************
Include "MapBasic.DEF"
Include "Menu.DEF"

	Declare Sub MapWasteDisposal
				
	Global PathVectorL As String
	Global PathBackdrop As String
	Global PathAccess As String
	Global DatabaseName As String

	Sub MapWasteDisposal

	OnError Goto Err_Manage

	Dim MapWinID As Integer

	Close All 
	
	Open Table PathVectorL + "Roof_MI.TAB" As Roof_MI

	Map From Roof_MI Max

	MapWinID = FrontWindow()

	Open Table PathBackdrop + "nauru050515.TAB" Interactive
	
	Add Map Auto Layer nauru050515

	Set Map Window MapWinID Zoom 1.0 Units "km" Center (166.9354, -0.504205)

'	++++++++++++++++ import Access Table +++++++++++++++++++++++++++++++++++++++++

	Register Table PathAccess + DatabaseName  
	Type ACCESS Table "TMP_RoofTank" 
	Into PathVectorL + "TMP_RoofTank.TAB"

	Open Table PathVectorL + "TMP_RoofTank.TAB" Interactive

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++ Link MapInfo and Access Table +++++++++++++++++++++++++++++++

	Select * from Roof_MI, TMP_RoofTank 
	where Roof_MI.Roof_ID = TMP_RoofTank.Roof_ID 
	into LinkTable

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Add to current mapper +++++++++++++++++++++++++++++++

	set map redraw off
	Add Map Layer LinkTable 
	set map redraw on

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Create thematic Map +++++++++++++++++++++++++++++++

	shade window MapWinID LinkTable with WasteDisposal values  
		"Cesspit" Brush (2,65535,16777215) Pen (1,2,0) ,
		"Composting" Brush (2,65280,16777215) Pen (1,2,0) ,
		"Communal septic" Brush (2,255,16777215) Pen (1,2,0) ,
		"Septic tank" Brush (2,16711935,16777215) Pen (1,2,0) ,
		"Soak away" Brush (2,16776960,16777215) Pen (1,2,0) ,
		"Other" Brush (2,16711680,16777215) Pen (1,2,0) ,
		"None" Brush (2,800,16777215) Pen (1,2,0) ,
		"Unknown" Brush (2,12632256,16777215) Pen (1,2,0)  
		default Brush (1,0,16777215) Pen (1,2,0)   
 
'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Create Map Legend +++++++++++++++++++++++++++++++

 	set legend window MapWinID layer prev 
		display on 
		shades on 
		symbols off 
		lines off 
		count on 
		title "Sanitation" Font ("Arial",1,10,0) 
		subtitle auto Font ("Arial",0,8,0) 
		ascending on ranges Font ("Arial",0,8,0) 
		auto display off ,
		auto display on ,
		auto display on ,
		auto display on ,
		auto display on ,
		auto display on ,
		auto display on ,
		auto display on ,
		auto display on  

	Set Window WIN_LEGEND 
		Position (0.5,3) Units "cm"
		Width 4.2 Units "cm"
		Height 4.2 Units "cm"

	Open Window Legend

	Run Menu Command M_QUERY_UNSELECT

	set map redraw off
	Set Map Layer LinkTable Label Font ("Arial",257,12,0,16777215) Overlap On
	Set Map Layer LinkTable Label Auto On 
	set map redraw on

		Res_Main:
			Exit Sub

		Err_Manage:
			note "Error: " + str$(Err()) + ", " + Error$()
			Resume Res_Main

	End sub
