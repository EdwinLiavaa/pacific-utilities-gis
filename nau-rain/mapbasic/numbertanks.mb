'***********************************************************
'*       NumberTanks                 Wolf 28.02.08         *
'*	  Modified: Edwin 25.06.08                              *
'*   This program                                          *
'*           - imports the TMP_RoofTank from Access        *
'*           - links Roof_MI to TMP_RoofTank               *
'*           - adds the linked tables to mapwindow         *
'*           - creates a thematic map from the linked TAB  *
'***********************************************************
Include "MapBasic.DEF"
Include "Menu.DEF"

	Declare Sub NumberTanks

	Global PathVectorL As String
	Global PathBackdrop As String
	Global PathAccess As String
	Global DatabaseName As String

	Sub NumberTanks

	OnError Goto Err_Manage

	Dim MapWinID As Integer

	Close All 
	
	Open Table PathVectorL + "Roof_MI.TAB" As Roof_MI

	Map From Roof_MI Max

	MapWinID = FrontWindow()

	Open Table PathBackdrop + "nauru050515.TAB" Interactive
	
	Add Map Auto Layer nauru050515

	Set Map Window MapWinID Zoom 1.0 Units "km" Center (166.9354, -0.504205)

'	++++++++++++++++ import Access Table +++++++++++++++++++++++++++++++++++++++++

	Register Table PathAccess + DatabaseName  
	Type ACCESS Table "TMP_RoofTank" 
	Into PathVectorL + "Roof_Main.TAB"

	Open Table PathVectorL + "Roof_Main.TAB" Interactive

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++ Link MapInfo and Access Table +++++++++++++++++++++++++++++++

	Select * from Roof_MI, Roof_Main
	where Roof_MI.Roof_ID = Roof_Main.Roof_ID 
	into LinkTable

	Select Sum(NumberOfTanks) from LinkTable into SumNumTanks
	
'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Add to current mapper +++++++++++++++++++++++++++++++

	set map redraw off
	Add Map Layer LinkTable 
	set map redraw on

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Create thematic Map +++++++++++++++++++++++++++++++

	shade window MapWinID LinkTable with NumberOfTanks ranges apply all use all Brush (2,65280,16777215)
	0: 0 Brush (2,16711680,16777215) Pen (1,2,0) ,
	1: 1 Brush (2,255,16777215) Pen (1,2,0) ,
	2: 2 Brush (2,65535,16777215) Pen (1,2,0) ,
	3: 3 Brush (2,16776960,16777215) Pen (1,2,0) ,
	4: 20 Brush (2,65280,16777215) Pen (1,2,0) 
	default Brush (2,16777215,16777215) Pen (1,2,0)
	  
'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Create Map Legend +++++++++++++++++++++++++++++++

	set legend window MapWinID layer prev 
	display on 
	shades on 
	symbols off 
	lines off 
	count on 
	title "No. of Water Tanks per Roof" Font ("Arial",1,10,0) 
	subtitle auto Font ("Arial",0,8,0) 
	ascending on 
	ranges Font ("Arial",0,8,0) 
	auto display off ,
	auto display on ,
	auto display on ,
	auto display on ,
	auto display on ,
	auto display on  

	Set Window WIN_LEGEND 
		Position (0.5,3) Units "cm"
		Width 4 Units "cm"
		Height 4 Units "cm"

	Open Window Legend

	Run Menu Command M_QUERY_UNSELECT

		Res_Main:
			Exit Sub

		Err_Manage:
			note "Error: " + str$(Err()) + ", " + Error$()
			Resume Res_Main

	End sub