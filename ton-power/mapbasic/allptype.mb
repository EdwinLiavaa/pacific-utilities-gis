'********************************************************
'*                                                      *
'*     AllPType.MBO                Edwin, 24.08.98      *
'*                                                      *
'*  This program dispalys all customer types in         *
'*  different colours.                                  *
'*                                                      *
'* ++++++++++++++++++++++++++++++++++++++++++++++++++++ *
'* required files:                                      *
'*                MAPBASIC.DEF                          *
'*			MENU.DEF                              *
'* ++++++++++++++++++++++++++++++++++++++++++++++++++++ *
'* required tables:                                     *
'*                  - customer.tab                      *
'*                   		                          *
'********************************************************

	include "C:\Program Files\MapInfo\MapBasic\MAPBASIC.DEF"
	include "C:\Program Files\MAPINFO\MAPBASIC\MENU.DEF"

	Declare Sub AllPType

	Global passw as String 
	Global MapPath as string
	Global CadaPath as String


'+++++++++++++++++++++++++++++++++++++++++++++++++++++++*

	Sub AllPType

	
	Dim  ivar1, ivar2, ivar3 As Integer
	Dim Map_Win_Id 		As Integer
	Dim i_lay_type		As SmallInt
	Dim iz, ln			As SmallInt
	Dim MapName			As String
	Dim phasety			As String
	Dim iphase 			As SmallInt
	Dim aRowId 			as Alias
	Dim aphase 			as Alias
	Dim aphaseName 		as Alias
	Dim sphaseType(1) 	as String
	Dim sphaseName(1)		as String
	Dim iCounter 		as Integer


	OnError goto Err_AllPType

' ++++++++++++++++++++++++++ close some toolbars ++++++++++++++++++++

	Alter ButtonPad "Tools" Hide
	Alter ButtonPad "Drawing" Hide
	Alter ButtonPad "Main" Show Fixed
	Alter ButtonPad "ODBC" Hide


' ++++++++++++++++++++++++++ close and open map layers ++++++++++++++

	
	MapName = MapPath +"CUSTOMER.TAB"
	Close All
	
	Open Table MapName As "Customer" password passw

	Map From Customer

	Mapname = Cadapath + "n_cad.tab"
	open table MapName as n_cad interactive

	add map layer n_cad

'+++++++++++++++++++++++++++  Getting window ID +++++++++++++++++++++

	Map_Win_id = FrontWindow()

'++++++  go out of program if operator does not know password +++++++

	If Map_Win_id = 0 Then
		Exit Sub
	End If


	Set Window Map_Win_id Max

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++++++++++++++++++++++++++ Shade statements +++++++++++++++++++++++++

	Map_Win_id= FrontWindow()
	if WindowInfo(Map_Win_id,WIN_INFO_TYPE)<>WIN_MAPPER then
		note "You must Select a Map Window first"
		Exit Sub
	End If

	ln = MapperInfo(Map_Win_id, MAPPER_INFO_LAYERS)
	iz = 1
	Do 
		i_lay_type = LayerInfo(Map_Win_id, iz, LAYER_INFO_TYPE)
		If i_lay_type = LAYER_INFO_TYPE_THEMATIC Then
			set map redraw off
		 	Set Map Layer iz Display Off
		 	set map redraw on
		End If
		iz = iz + 1

	Loop While iz <= ln


	Shade Window Map_Win_id "Customer" with PHA_TYPE values  
		"01" Symbol (33,RED,12,"MapInfo Real Estate",257,0) ,
		"02" Symbol (33,YELLOW,12,"MapInfo Real Estate",257,0) ,
		"03" Symbol (33,BLUE,12,"MapInfo Real Estate",257,0) ,
		"04" Symbol (33,CYAN,12,"MapInfo Real Estate",257,0) ,
		"05" Symbol (33,MAGENTA,12,"MapInfo Real Estate",257,0) 
		default Symbol (33,0,12,"MapInfo Real Estate",257,0)

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

'+++++++++++++++++++++++++ Legend statements +++++++++++++++++++++++++

	Set Legend Window Map_Win_id layer prev display on shades off symbols on 
		lines off count on title "Phases Customers Connected to" 
		Font ("MS Sans Serif",0,10,0) subtitle "" 
		Font ("MS Sans Serif",0,8,0)ascending on ranges 
		Font ("MS Sans Serif",0,8,0) 
		"?  = unknown or disconnect" display on ,
		"01 = Single Phase RED" display on ,
		"02 = Single Phase YELLOW" display on ,
		"03 = Single Phase BLUE" display on ,
		"04 = Three Phases Non CT" display on ,
		"05 = Three Phases CT" display on 


	Set Window WIN_LEGEND 
	Position (1,4) Units "cm"
	Width 7 Units "cm"
	Height 5 Units "cm"

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++

	Open Window Legend

Res_AllPType:
	Exit Sub

Err_AllPType:
	note "AllPType: "+str$(err())+" "+error$()
	Resume Next

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++

	End Sub
