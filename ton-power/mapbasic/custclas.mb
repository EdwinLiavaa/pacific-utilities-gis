'********************************************************
'*                                                      *
'*     CustClas.MBO                Edwin, 24.08.98      *
'*                                                      *
'*  This program dispalys customers according to which  *
'*  class they belong to i.e. domestic, commercial, etc.*
'*                                                      *
'* ++++++++++++++++++++++++++++++++++++++++++++++++++++ *
'* required files:                                      *
'*                MAPBASIC.DEF                          *
'*			MENU.DEF                              *
'* ++++++++++++++++++++++++++++++++++++++++++++++++++++ *
'* required tables:                                     *
'*                  - customer.tab                      *
'*                  - class.tab                         *
'********************************************************

	include "C:\Program Files\MapInfo\MapBasic\MAPBASIC.DEF"
	include "C:\Program Files\MAPINFO\MAPBASIC\MENU.DEF"

	Declare Sub CustClas

	Global passw as String 
	Global MapPath as string
	Global CadaPath as String


'+++++++++++++++++++++++++++++++++++++++++++++++++++++++*

	Sub CustClas

	
	Dim  ivar1, ivar2, ivar3 As Integer
	Dim Map_Win_Id 		As Integer
	Dim i_lay_type		As SmallInt
	Dim iz, ln			As SmallInt
	Dim MapName			As String
	Dim classty			As String
	Dim iclass 			As SmallInt
	Dim aRowId 			as Alias
	Dim aclass 			as Alias
	Dim aclassName 		as Alias
	Dim sclassType(1) 	as String
	Dim sclassName(1)		as String
	Dim iCounter 		as Integer


	OnError goto Err_CustClas

' ++++++++++++++++++++++++++ close some toolbars ++++++++++++++++++++

	Alter ButtonPad "Tools" Hide
	Alter ButtonPad "Drawing" Hide
	Alter ButtonPad "Main" Show Fixed
	Alter ButtonPad "ODBC" Hide


' ++++++++++++++++++++++++++ close and open map layers ++++++++++++++


	MapName = MapPath +"CUSTOMER.TAB"

	Close All
	
	Open Table MapName As "Customer" password passw

	Map From Customer MAX

	Mapname = Cadapath + "n_cad.tab"
	open table MapName as n_cad interactive

	add map layer n_cad


'+++++++++++++++++++++++++++  Getting window ID +++++++++++++++++++++

	Map_Win_id = FrontWindow()

'++++++  go out of program if operator does not know password +++++++

	If Map_Win_id = 0 Then
		Exit Sub
	End If

' ++++++++++++Store Data into Array Variable from Table+++++++++++++

	MapName = MapPath +"class.tab"
	open table MapName password passw as "class"

	aRowId="Class.rowid"
	aClass="Class.C_CLASS"
	aClassName="Class.NAME"

	Fetch First from class		' cursor to first record
	iCounter=1
	Do While NOT EOT( Class )
		Redim sClassType(iCounter)
		Redim sClassName(iCounter)
		if iCounter=1 then
			sClassType(iCounter)=aClass	
			sClassName(iCounter)=aClassName
			iCounter=2
		else
			if sClassType(iCounter-1)<>aClass then
				sClassType(iCounter)=aClass
				sClassName(iCounter)=aClassName
				iCounter=iCounter+1
			end if	
		end if
		Fetch Next from Class
	Loop
	Close Table Class
' +++++++++++++++++++  Dialog ++++++++++++++++++++++++++++++++++++++++


	Dialog

	Title "select type:"

	Control StaticText
		Title "select customer class:"
		Position 5, 10

	Control PopUpMenu
		Title From Variable sclassName
   		Value 1
		Position 20,20 Width 60
		Into iclass
	
	Control OKButton
		Position 20, 40
	Control CancelButton
		Position 65, 40

	If not CommandInfo(CMD_INFO_DLG_OK) Then 	'The user just cancelled.
  		Exit Sub
	End If 
 
'++++++++++++++++ creating the right variable +++++++++++++++++++++++++
 
		
	classty = sClassType(iclass)


'++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

'++++++++++++++++++++++++++ Shade statements +++++++++++++++++++++++++

	Shade Window Map_Win_Id "Customer" 
		with C_CLASS Values classty 
		Symbol (34,RED,8) 
		default Symbol (34,BLACK,8)

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

'+++++++++++++++++++++++++ View Entire Layer +++++++++++++++++++++++++

	
	Set Map Window  Map_Win_Id Zoom Entire Layer "Customer"

'+++++++++++++++++++++++++ Legend Statements +++++++++++++++++++++++++

	Set Legend Window Map_Win_Id layer prev 
		display on 
		shades off 
		symbols on 
		lines off 
		count on 
		title "Select Customer Class" 
		Font ("MS Sans Serif",0,10,0) 
		subtitle "" Font ("MS Sans Serif",0,8,0) 
		ascending on 
		ranges Font ("MS Sans Serif",0,8,0) "all others" 
		display on ,
		sClassName(iclass) display on
 
	Set Window WIN_LEGEND 
	Position (1,4) Units "cm"
	Width 6 Units "cm"
	Height 3 Units "cm"

	Open Window Legend

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

	Set Window Map_Win_id Max

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

Res_CustClas:
	Exit Sub

Err_CustClas:
	note "CustClas: "+str$(err())+" "+error$()
	Resume Next

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++

	End Sub
