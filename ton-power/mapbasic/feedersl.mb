'********************************************************
'*                                                      *
'*     FeedersL.MBO                Edwin, 20.08.98      *
'*                                                      *
'*  This program dispalys customers according to which  *
'*  phase type their connected.	                    *
'*                                                      *
'* ++++++++++++++++++++++++++++++++++++++++++++++++++++ *
'* required files:                                      *
'*                MAPBASIC.DEF                          *
'*			MENU.DEF                              *
'* ++++++++++++++++++++++++++++++++++++++++++++++++++++ *
'* required tables:                                     *
'*                  - Lines.tab                         *
'*                  - circuit.tab                       *
'********************************************************

	include "C:\Program Files\MapInfo\MapBasic\MAPBASIC.DEF"
	include "C:\Program Files\MAPINFO\MAPBASIC\MENU.DEF"

	Declare Sub FeedersL

	Global passw as String 
	Global MapPath as string
	Global CadaPath as String



'+++++++++++++++++++++++++++++++++++++++++++++++++++++++*

	Sub FeedersL

	
	Dim  ivar1, ivar2, ivar3 As Integer
	Dim Map_Win_Id 		As Integer
	Dim i_lay_type		As SmallInt
	Dim iz, ln			As SmallInt
	Dim MapName			As String
	Dim circuitty		As String
	Dim icircuit		As SmallInt
	Dim aRowId 			as Alias
	Dim acircuit		as Alias
	Dim acircuitName 		as Alias
	Dim scircuitType(1) 	as String
	Dim scircuitName(1)	as String
	Dim iCounter 		as Integer
	

	OnError goto Err_FeedersL

' ++++++++++++++++++++++++++ close some toolbars ++++++++++++++++++++

	Alter ButtonPad "Tools" Hide
	Alter ButtonPad "Drawing" Hide
	Alter ButtonPad "Main" Show Fixed
	Alter ButtonPad "ODBC" Hide


' ++++++++++++++++++++++++++ close and open map layers ++++++++++++++

	
	MapName = MapPath +"LINES.TAB"	'###### path may change

	Close All
	
	Open Table MapName As "Lines" password passw

	Map From Lines MAX

	Mapname = Cadapath + "n_cad.tab"
	open table MapName as n_cad interactive

	add map layer n_cad

'+++++++++++++++++++++++++++  Getting window ID +++++++++++++++++++++

	Map_Win_id = FrontWindow()

'++++++  go out of program if operator does not know password +++++++

	If Map_Win_id = 0 Then
		Exit Sub
	End If

' ++++++++++++Store Data into Array Variable from Table+++++++++++++

	MapName = MapPath +"circuit"	
	open table MapName password passw as "circuit"

	aRowId="circuit.rowid"
	acircuit="circuit.CIR_CODE"
	acircuitName="circuit.CIR_NAME"

	Fetch First from circuit		' cursor to first record
	iCounter=1
	Do While NOT EOT( Circuit )
		Redim sCircuitType(iCounter)
		Redim sCircuitName(iCounter)
		if iCounter=1 then
			sCircuitType(iCounter)=aCircuit
			sCircuitName(iCounter)=aCircuitName
			iCounter=2
		else
			if sCircuitType(iCounter-1)<>aCircuit then
				sCircuitType(iCounter)=aCircuit
				sCircuitName(iCounter)=aCircuitName
				iCounter=iCounter+1
			end if	
		end if
		Fetch Next from Circuit
	Loop
	Close Table Circuit

' +++++++++++++++++++  Dialog ++++++++++++++++++++++++++++++++++++++++


	Dialog

	Title "select type:"

	Control StaticText
		Title "select circuit:"
		Position 5, 10

	Control PopUpMenu
		Title From Variable scircuitName
   		Value 1
		Position 20,20 Width 60
		Into icircuit
	
	Control OKButton
		Position 20, 40
	Control CancelButton
		Position 65, 40

	If not CommandInfo(CMD_INFO_DLG_OK) Then 	'The user just cancelled.
  		Exit Sub
	End If 
 
'++++++++++++++++ creating the right variable +++++++++++++++++++++++++
 
		
	circuitty = sCircuitType(icircuit)


'++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

'++++++++++++++++++++++++++ Shade statements +++++++++++++++++++++++++

	Shade Window Map_Win_Id "Lines" 
		with CIR_CODE Values circuitty 
		Line  (2, 2, RED) 
		default Pen (2, 2, BLACK)

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

'+++++++++++++++++++++++++ View Entire Layer +++++++++++++++++++++++++

	
	Set Map Window  Map_Win_Id Zoom Entire Layer "Lines"

'+++++++++++++++++++++++++ Legend Statements +++++++++++++++++++++++++

	Set Legend Window Map_Win_Id layer prev 
		display on 
		shades off 
		symbols on 
		lines off 
		count on 
		title "Select Circuit" 
		Font ("MS Sans Serif",0,10,0) 
		subtitle "" Font ("MS Sans Serif",0,8,0) 
		ascending on 
		ranges Font ("MS Sans Serif",0,8,0) "all others" 
		display on ,
		sCircuitName(icircuit) display on
 
	Set Window WIN_LEGEND 
	Position (1,4) Units "cm"
	Width 6 Units "cm"
	Height 3 Units "cm"

	Open Window Legend

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

	Set Window Map_Win_id Max

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

Res_FeedersL:
	Exit Sub

Err_FeedersL:
	note "FeedersL: "+str$(err())+" "+error$()
	Resume Next

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++

	End Sub
