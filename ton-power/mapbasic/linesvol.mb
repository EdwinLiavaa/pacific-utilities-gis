'********************************************************
'*                                                      *
'*     LinesVol.MBO                Edwin, 24.08.98      *
'*                                                      *
'*  This program dispalys all line types in             *
'*  different colours according to the voltage.         *
'*                                                      *
'* ++++++++++++++++++++++++++++++++++++++++++++++++++++ *
'* required files:                                      *
'*                MAPBASIC.DEF                          *
'*			MENU.DEF                              *
'* ++++++++++++++++++++++++++++++++++++++++++++++++++++ *
'* required tables:                                     *
'*                  - Lines.tab                         *
'*                    		                          *
'********************************************************

	include "C:\Program Files\MapInfo\MapBasic\MAPBASIC.DEF"
	include "C:\Program Files\MAPINFO\MAPBASIC\MENU.DEF"

	Global passw as String 
	Global MapPath as string
	Global CadaPath as String


	Declare Sub LinesVol

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++*

	Sub LinesVol

	
	Dim  ivar1, ivar2, ivar3 As Integer
	Dim Map_Win_Id 		As Integer
	Dim i_lay_type		As SmallInt
	Dim iz, ln			As SmallInt
	Dim mapname			As String


	OnError goto Err_LinesVol

' ++++++++++++++++++++++++++ close some toolbars ++++++++++++++++++++

	Alter ButtonPad "Tools" Hide
	Alter ButtonPad "Drawing" Hide
	Alter ButtonPad "Main" Show Fixed
	Alter ButtonPad "ODBC" Hide


' ++++++++++++++++++++++++++ close and open map layers ++++++++++++++

	
	MapName = MapPath +"LINES.TAB"    '###### path may change

	Close All
	
	Open Table MapName As "Lines" password passw

	Map From Lines Max
	Mapname = Cadapath + "n_cad.tab"
	open table MapName as n_cad interactive

	add map layer n_cad

'+++++++++++++++++++++++++++  Getting window ID +++++++++++++++++++++

	Map_Win_id = FrontWindow()

'++++++  go out of program if operator does not know password +++++++

	If Map_Win_id = 0 Then
		Exit Sub
	End If


	Set Window Map_Win_id Max

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++++++++++++++++++++++++++ Shade statements +++++++++++++++++++++++++

	Map_Win_id= FrontWindow()
	if WindowInfo(Map_Win_id,WIN_INFO_TYPE)<>WIN_MAPPER then
		note "You must Select a Map Window first"
		Exit Sub
	End If

	ln = MapperInfo(Map_Win_id, MAPPER_INFO_LAYERS)
	iz = 1
	Do 
		i_lay_type = LayerInfo(Map_Win_id, iz, LAYER_INFO_TYPE)
		If i_lay_type = LAYER_INFO_TYPE_THEMATIC Then
			set map redraw off
		 	Set Map Layer iz Display Off
		 	set map redraw on
		End If
		iz = iz + 1

	Loop While iz <= ln


	Shade Window Map_Win_id "Lines" with VOLT values  
		11000 Line (2, 2, RED) ,
		6600 Line (2, 2, YELLOW) ,
		415 Line (2, 2, BLUE) ,
		240 Line (2, 2, GREEN) 		

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

'+++++++++++++++++++++++++ Legend statements +++++++++++++++++++++++++

	Set Legend Window Map_Win_id layer prev 
		display on  
		shades off 
		symbols off
		lines on 
   		count on 
		title "Voltage of Lines" 
		Font ("MS Sans Serif",0,10,0) subtitle "" 
		Font ("MS Sans Serif",0,8,0)ascending off ranges 
		Font ("MS Sans Serif",0,8,0) 
		"?  = unknown or disconnect" display on ,
		"240" display on ,
		"415" display on ,
		"6600" display on ,
		"11000" display on 		

	Set Window WIN_LEGEND 
	Position (1,4) Units "cm"
	Width 7 Units "cm"
	Height 3 Units "cm"

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++

	Open Window Legend

Res_LinesVol:
	Exit Sub

Err_LinesVol:
	note "LinesVol: "+str$(err())+" "+error$()
	Resume Next

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++

	End Sub
