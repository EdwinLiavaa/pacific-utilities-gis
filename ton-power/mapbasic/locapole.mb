'********************************************************
'*                                                      *
'*     LocaPole.MBO                Edwin, 20.08.98      *
'*                                                      *
'*  This program asks the operator to type in a pole ID *
'*  locates the pole, centres the pole and zooms in.    *
'*                                                      *
'* ++++++++++++++++++++++++++++++++++++++++++++++++++++ *
'* required files:                                      *
'*                MAPBASIC.DEF                          *
'*			MENU.DEF                              *
'* ++++++++++++++++++++++++++++++++++++++++++++++++++++ *
'* required tables:                                     *
'*                  - poles.tab                         *
'*                                                      *
'********************************************************

	include "C:\Program Files\MapInfo\MapBasic\MAPBASIC.DEF"
	include "C:\Program Files\MAPINFO\MAPBASIC\MENU.DEF"

	Global passw as String 
	Global MapPath as string
	Global CadaPath as String


	Declare Sub LocaPole

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++*

	Sub LocaPole


	
	Dim  ivar1, ivar2, ivar3 	As Integer
	Dim poleno 				As String
	Dim Map_Win_Id 			As Integer
	Dim ixv, iyv			As Float
	Dim iz, ln 				As SmallInt
	Dim i_lay_type			As SmallInt
	Dim MapName				As String



	OnError goto Err_LocaPole

' ++++++++++++++++++++++++++ close some toolbars ++++++++++++++++++++

	Alter ButtonPad "Tools" Hide
	Alter ButtonPad "Drawing" Hide
	Alter ButtonPad "Main" Show Fixed
	Alter ButtonPad "ODBC" Hide


' ++++++++++++++++++++++++++ close and open map layers ++++++++++++++
'               #######



	MapName = MapPath +"POLES.TAB"

	Close All
	
	Open Table MapName As "Poles" Password passw  

	Map From Poles

	Mapname = Cadapath + "n_cad.tab" 
	open table MapName as n_cad interactive

	add map layer n_cad
'+++++++++++++++++++++++++++  Getting window ID +++++++++++++++++++++

	Map_Win_id = FrontWindow()


	If Map_Win_id = 0 Then
		Exit Sub
	End If


' +++++++++++++++++++  Dialog ++++++++++++++++++++++++++++++++++++++++

	Dialog

	Title "Pole ID"

	Control StaticText
		Title "type in pole identification:"
		Position 5, 10

	Control EditText
		Position 20,20 Width 40
		Into poleno
	
	Control OKButton
		Position 5, 40

	Control CancelButton
		Position 45, 40

	If not CommandInfo(CMD_INFO_DLG_OK) Then 	'The user just cancelled.
  		Exit Sub
	End If

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

	Select * From Poles Where POLE_ID=poleno

	Run Menu Command M_QUERY_FIND_SELECTION


		ixv = CentroidX(Poles.obj)
		iyv = CentroidY(Poles.obj)

'++++++++++++++++++++++++++ Shade statements +++++++++++++++++++++++++

	Shade Window Map_Win_Id "Poles" with POLE_ID 
		Values poleno 
		Symbol (34,RED,8) 
		default Symbol (34,BLACK,8)

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

'+++++++++++++++++ Set Centre and Zoom of Map ++++++++++++++++++++++++

	Set Map Window Map_Win_Id
		Center (ixv,iyv)
		Zoom 900 Units "m"

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

	Set Window Map_Win_id Max

'+++++++++++++++++++++++++ Legend statements +++++++++++++++++++++++++

	Set Legend Window Map_Win_Id layer prev display on 
		shades off 
		symbols on 
		lines off 
		count on 
		title "Pole number" 
		Font ("MS Sans Serif",0,10,0) subtitle "" 
		Font ("MS Sans Serif",0,8,0) ascending on ranges 
		Font ("MS Sans Serif",0,8,0) "all others" display on ,
		poleno display on
 

	Set Window WIN_LEGEND 
	Position (1,4) Units "cm"
	Width 4 Units "cm"
	Height 2 Units "cm"

	Open Window Legend


'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

Res_LocaPole:
	Exit Sub

Err_LocaPole:
	note "LocaPole: "+str$(err())+" "+error$()
	Resume Next

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++


	End Sub