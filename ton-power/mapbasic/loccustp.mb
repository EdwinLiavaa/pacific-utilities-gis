'********************************************************
'*                                                      *
'*     LocCustP.MBO                Edwin, 24.08.98      *
'*                                                      *
'*  This program asks the operator to type in a Pole Id *
'*  number and zoom in, display customers connected	  *	
'*  to that Pole in red colour.        			  *
'* ++++++++++++++++++++++++++++++++++++++++++++++++++++ *
'* required files:                                      *
'*                MAPBASIC.DEF                          *
'*			MENU.DEF                              *
'* ++++++++++++++++++++++++++++++++++++++++++++++++++++ *
'* required tables:                                     *
'*                  - customers.tab                     *
'*                                                      *
'********************************************************

	include "C:\Program Files\MapInfo\MapBasic\MAPBASIC.DEF"
	include "C:\Program Files\MAPINFO\MAPBASIC\MENU.DEF"

	Global passw as String 
	Global MapPath as string
	Global CadaPath as String

	Declare Sub LocCustP
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++*

	Sub LocCustP

	
	Dim  ivar1, ivar2, ivar3 	As Integer
	Dim polid				As String
	Dim Map_Win_Id 			As Integer
	Dim ixv, iyv			As Float
	Dim iz, ln 				As SmallInt
	Dim i_lay_type			As SmallInt
	Dim MapName				As String



	OnError goto Err_LocCustP

' ++++++++++++++++++++++++++ close some toolbars ++++++++++++++++++++

	Alter ButtonPad "Tools" Hide
	Alter ButtonPad "Drawing" Hide
	Alter ButtonPad "Main" Show Fixed
	Alter ButtonPad "ODBC" Hide


' ++++++++++++++++++++++++++ close and open map layers ++++++++++++++
' for definition of passw see in password.mbo              #######
	
	MapName = MapPath +"CUSTOMER.TAB"

	Close All
	
	Open Table MapName As "Customer" password pwd 

	Map From Customer

	Mapname = Cadapath + "n_cad.tab"
	open table MapName as n_cad interactive

	add map layer n_cad

'+++++++++++++++++++++++++++  Getting window ID +++++++++++++++++++++

	Map_Win_id = FrontWindow()


	If Map_Win_id = 0 Then
		Exit Sub
	End If


' +++++++++++++++++++  Dialog ++++++++++++++++++++++++++++++++++++++++

	Dialog

	Title "Pole Id."

	Control StaticText
		Title "type in Pole Id.:"
		Position 5, 10

	Control EditText
		Position 20,20 Width 40
		Into polid
	
	Control OKButton
		Position 5, 40

	Control CancelButton
		Position 45, 40

	If not CommandInfo(CMD_INFO_DLG_OK) Then 	'The user just cancelled.
  		Exit Sub
	End If

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

	Select * From Customer Where POLE_ID = polid

	Run Menu Command M_QUERY_FIND_SELECTION


		ixv = CentroidX(Customer.obj)
		iyv = CentroidY(Customer.obj)

'++++++++++++++++++++++++++ Shade statements +++++++++++++++++++++++++

	Shade Window Map_Win_Id "Customer" with POLE_ID 
		Values polid
		Symbol (34,RED,8) 
		default Symbol (34,BLACK,8)

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

'+++++++++++++++++ Set Centre and Zoom of Map ++++++++++++++++++++++++

	Set Map Window Map_Win_Id
		Center (ixv,iyv)
		Zoom 900 Units "m"

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

	Set Window Map_Win_id Max

'+++++++++++++++++++++++++ Legend statements +++++++++++++++++++++++++

	Set Legend Window Map_Win_Id layer prev display on 
		shades off 
		symbols on 
		lines off 
		count on 
		title "Pole Id."
		Font ("MS Sans Serif",0,10,0) subtitle "" 
		Font ("MS Sans Serif",0,8,0) ascending on ranges 
		Font ("MS Sans Serif",0,8,0) "all others" display on ,
		polid display on
 

	Set Window WIN_LEGEND 
	Position (1,4) Units "cm"
	Width 4 Units "cm"
	Height 2 Units "cm"

	Open Window Legend


'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

Res_LocCustP:
	Exit Sub

Err_LocCustP:
	note "LocCustL: "+str$(err())+" "+error$()
	Resume Next

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++


	End Sub