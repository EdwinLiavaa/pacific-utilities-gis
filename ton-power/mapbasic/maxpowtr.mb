'********************************************************
'*                                                      *
'*     MaxPowTr.MBO                Paula, 24.08.98      *
'*                                                      *
'*  This program displays The Transformers by range of  *
'*  Maximum Power.                                      *
'*                                                      *
'* ++++++++++++++++++++++++++++++++++++++++++++++++++++ *
'* required files:                                      *
'*		MAPBASIC.DEF                                *
'*		MENU.DEF                                    *
'* ++++++++++++++++++++++++++++++++++++++++++++++++++++ *
'* required tables:                                     *
'*                  - Tralist.tab                       *
'*                  - Location.tab                      *
'********************************************************


	include "C:\Program Files\mapinfo\mapbasic\MAPBASIC.DEF"
	include "C:\Program Files\MAPINFO\MAPBASIC\MENU.DEF"

	Global passw as String 
	Global MapPath as string
	Global CadaPath as String


	Declare Sub MaxPowTr


'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	Sub MaxPowTr

	Dim Map_Win_Id	As Integer
	Dim iz, ln		As SmallInt
	Dim i_lay_type	As SmallInt
	Dim MapName		As String

' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

	OnError Goto Err_PowerMax

' ++++++++++++++++++++++++ Open Table Statement ++++++++++++++++++++++++++++++++++++

	MapName = MapPath +"Tralist.tab"
	Open table MapName Password passw as Tralist 
	MapName = MapPath +"Location.tab"
	Open table MapName Password passw as Location 

	Select * from Tralist,Location Where Tralist.LOCAT_NO=Location.LOCAT_NO
		into Transformers NoSelect

	Map From Transformers Max

	Mapname = Cadapath + "n_cad.tab"
	open table MapName as n_cad interactive

	add map layer n_cad
'+++++++++++++++++++++++++ Get Windows ID ++++++++++++++++++++++++++++++++++++++++++++++


	Map_Win_id= FrontWindow()
	if WindowInfo(Map_Win_id,WIN_INFO_TYPE)<>WIN_MAPPER then
		note "You must Select a Map Window first"
		Exit Sub
	End If

	ln = MapperInfo(Map_Win_id, MAPPER_INFO_LAYERS)
	iz = 1

	Do 
		i_lay_type = LayerInfo(Map_Win_id, iz, LAYER_INFO_TYPE)
		If i_lay_type = LAYER_INFO_TYPE_THEMATIC Then
			set map redraw off
		 	Set Map Layer iz Display Off
		 	set map redraw on
		End If
		iz = iz + 1

	Loop While iz <= ln

	

'++++++++++++++++++++++++++ Shade statements +++++++++++++++++++++++++

	Shade Window Map_Win_Id "Transformers" with KVA Ranges  
		1:16 Symbol (37,RED,12,"MapInfo Symbols",257,0) ,
		16:31 Symbol (37,32768,12,"MapInfo Symbols",257,0) ,
		31:51 Symbol (37,GREEN,12,"MapInfo Symbols",257,0) ,
		51:101 Symbol (37,BLUE,12,"MapInfo Symbols",257,0) ,
		101:201 Symbol (37,YELLOW,12,"MapInfo Symbols",257,0) ,
		201:501 Symbol (37,6711935,12,"MapInfo Symbols",257,0) ,
		501:1001 Symbol (37,6744576,12,"MapInfo Symbols",257,0) 
		default Symbol (37,0,12,"MapInfo Symbols",257,0)

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

'+++++++++++++++++++++++++ Legend statements +++++++++++++++++++++++++

	Set Legend Window Map_Win_Id layer prev display on shades off symbols on 
		lines off count on title "KVA grouping of transformers" 
		Font ("MS Sans Serif",0,10,0) subtitle "" 
		Font ("MS Sans Serif",0,8,0) ascending on ranges 
		Font ("MS Sans Serif",0,8,0) 
		"0 or ? or >1000 " display on ,
		"1 -   15 " display on ,
		"15 -   30 " display on ,
		"30 -   50 " display on ,
		"50 -   100 " display on ,
		"100 -   200 " display on ,
		"200 -   500 " display on ,
		"500 -  1000 " display on 



	Set Window WIN_LEGEND 
	Position (1,4) Units "cm"
	Width 6 Units "cm"
	Height 6 Units "cm"

	Open Window Legend

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

Res_PowerMax:
	Exit Sub

Err_PowerMax:
	note "PowerMax: "+str$(err())+" "+error$()
	Resume Res_PowerMax

End Sub

