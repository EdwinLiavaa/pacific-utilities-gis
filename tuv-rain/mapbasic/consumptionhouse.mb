'***********************************************************
'*       Consumptionhouse            Wolf    12.11.07      *
'*   Modified by Edwin 25.06.08                            *
'*   This program                                          *
'*           - imports the TMP_RoofDetails from Access     *
'*           - links Roof_MI to TMP_RoofDetails            *
'*           - adds the linked tables to mapwindow         *
'*           - creates a thematic map from the linked TAB  *
'***********************************************************
Include "MapBasic.DEF"
Include "Menu.DEF"

	Declare Sub Consumptionhouse

	Global PathVectorL As String
	Global PathBackdrop As String
	Global PathAccess As String
	Global DatabaseName As String

	Sub Consumptionhouse

	OnError Goto Err_Manage

	Dim MapWinID As Integer

	Close All 
	
	Open Table PathVectorL + "Roof_MI.TAB" As Roof_MI

	Map From Roof_MI Max

	MapWinID = FrontWindow()

	Open Table PathBackdrop + "funafuti.TAB" Interactive

	Add Map Auto Layer funafuti

	Set Map Window MapWinID Zoom 0.6 Units "km" Center (179.1994, -8.518482)

'	++++++++++++++++ import Access Table +++++++++++++++++++++++++++++++++++++++++
	Register Table PathAccess + DatabaseName   
	Type ACCESS Table "TMP_RoofDetails" 
	Into PathVectorL + "TMP_RoofDetails.TAB"

	Open Table PathVectorL + "TMP_RoofDetails.TAB" Interactive

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++ Link MapInfo and Access Table +++++++++++++++++++++++++++++++

	Select * from Roof_MI, TMP_RoofDetails 
	where Roof_MI.Roof_ID = TMP_RoofDetails.Roof_ID 
	into LinkTable

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Add to current mapper +++++++++++++++++++++++++++++++

	set map redraw off
	Add Map Layer LinkTable
	set map redraw on

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Create thematic Map +++++++++++++++++++++++++++++++
  
 	shade window MapWinID LinkTable with WaterDemandDay ranges apply all 
		use color Brush (2,16711680,16777215)  
		0: 0 Brush (2,16711680,16777215) Pen (1,2,0) ,
		0: 120 Brush (2,12582976,16777215) Pen (1,2,0) ,
		120: 240 Brush (2,8388736,16777215) Pen (1,2,0) ,
		240: 480 Brush (2,4194496,16777215) Pen (1,2,0) ,
		480: 5000 Brush (2,255,16777215) Pen (1,2,0) 
		default Brush (2,16777215,16777215) Pen (1,2,0) 
 
'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Create Map Legend +++++++++++++++++++++++++++++++

 	set legend window MapWinID layer prev 
		display on 
		shades on 
		symbols off 
		lines off 
		count on 
		title "Water Consumption per Day [litre]" Font ("Arial",1,12,0) 
		subtitle "House without ID = no survey" Font ("Arial",0,8,0) 
		ascending off ranges Font ("Arial",0,8,0) 
		auto display off ,
		auto display on ,
		auto display on ,
		auto display on ,
		auto display on ,
		auto display on  

	Set Window WIN_LEGEND 
		Position (0.5,3) Units "cm"
		Width 8 Units "cm"
		Height 4 Units "cm"

	Open Window Legend

	Run Menu Command M_QUERY_UNSELECT

	set map redraw off
	Set Map Layer LinkTable Label Font ("Arial",257,12,0,16777215) Overlap On
	Set Map Layer LinkTable Label Auto On 
	Set Map Layer Roof_MI Display Global Global Pen (2,2,0)  Global Brush (6,16711680,16777215)	
	set map redraw on

		Res_Main:
			Exit Sub

		Err_Manage:
			note "Error: " + str$(Err()) + ", " + Error$()
			Resume Res_Main

	End sub