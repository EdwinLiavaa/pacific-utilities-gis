'***********************************************************
'*       GutterPercent           Wolf    12.11.07          *
'*   Modified: Edwin 28.09.08                              *
'*   This program                                          *
'*           - imports the TMP_RoofTank from Access        *
'*           - links Roof_MI to TMP_RoofTank               *
'*           - adds the linked tables to mapwindow         *
'*           - creates a thematic map from the linked TAB  *
'***********************************************************
Include "MapBasic.DEF"
Include "Menu.DEF"

	Declare Sub GutterPercent
	
	Global PathVectorL As String
	Global PathBackdrop As String
	Global PathAccess As String
	Global DatabaseName As String

	Sub GutterPercent

	OnError Goto Err_Manage

	Dim MapWinID As Integer

	Close All 
	
	Open Table PathVectorL + "Roof_MI.TAB" As Roof_MI

	Map From Roof_MI Max

	MapWinID = FrontWindow()

	Open Table PathBackdrop + "funafuti.TAB" Interactive

	Add Map Auto Layer funafuti

	Set Map Window MapWinID Zoom 0.6 Units "km" Center (179.1994, -8.518482)

'	++++++++++++++++ import Access Table +++++++++++++++++++++++++++++++++++++++++

	Register Table PathAccess + DatabaseName   
	Type ACCESS Table "TMP_GutterDetails02" 
	Into PathVectorL + "TMP_GutterDetails02.TAB"

	Open Table PathVectorL + "TMP_GutterDetails02" Interactive

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++ Link MapInfo and Access Table +++++++++++++++++++++++++++++++

	Select * from Roof_MI, TMP_GutterDetails02 
	where Roof_MI.Roof_ID = TMP_GutterDetails02.Roof_ID 
	into LinkTable

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Add to current mapper +++++++++++++++++++++++++++++++

	set map redraw off
	Add Map Layer LinkTable
	set map redraw on

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Create thematic Map +++++++++++++++++++++++++++++++

		shade window MapWinID LinkTable with RoofCapture values  
		0 Brush (2,16711680,16777215) Pen (1,2,0) ,
		25 Brush (2,65280,16777215) Pen (1,2,0) ,
		50 Brush (2,255,16777215) Pen (1,2,0) ,
		75 Brush (2,16711935,16777215) Pen (1,2,0) ,
		100 Brush (2,16776960,16777215) Pen (1,2,0) 
		default Brush (1,0,16777215) Pen (1,2,0)   

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Create Map Legend +++++++++++++++++++++++++++++++

 	set legend window MapWinID layer prev 
		display on 
		shades on 
		symbols off 
		lines off 
		count on 
		title "Roof % Captured" Font ("Arial",1,11,0) 
		subtitle "(roof without number = no survey)" Font ("Arial",0,8,0) 
		ascending on 
		ranges Font ("Arial",0,8,0) 
		auto display off ,
		"  0 %" display on ,
		" 25 %" display on ,
		" 50 %" display on ,
		" 75 %" display on ,
		"100 %" display on 

	Set Window WIN_LEGEND 
		Position (0.5,3) Units "cm"
		Width 5.4 Units "cm"
		Height 4 Units "cm"

	Open Window Legend

	Run Menu Command M_QUERY_UNSELECT

	set map redraw off
	Set Map Layer LinkTable Label Font ("Arial",257,12,0,16777215) Overlap On
	Set Map Layer LinkTable Label Auto On 
   Set Map Layer Roof_MI Display Global Global Pen (2,2,0)  Global Brush (6,16711680,16777215)
	set map redraw on

		Res_Main:
			Exit Sub

		Err_Manage:
			note "Error: " + str$(Err()) + ", " + Error$()
			Resume Res_Main

	End sub