'***********************************************************
'*       MapStoragePerson         Wolf   12.11.07          *
'*   Modified: Edwin 28.09.08                              *
'*   This program                                          *
'*           - imports the TMP_RoofDetails from Access     *
'*           - links Roof_MI to TMP_RoofDetails            *
'*           - adds the linked tables to mapwindow         *
'*           - creates a thematic map from the linked TAB  *
'***********************************************************
Include "MapBasic.DEF"
Include "Menu.DEF"

	Declare Sub MapStoragePerson
	
	Global PathVectorL As String
	Global PathBackdrop As String
	Global PathAccess As String
	Global DatabaseName As String

	Sub MapStoragePerson

	OnError Goto Err_Manage

	Dim MapWinID As Integer

	Close All
 
	Open Table PathVectorL + "Roof_MI.TAB" As Roof_MI

	Map From Roof_MI Max

	MapWinID = FrontWindow()

	Open Table PathBackdrop + "funafuti.TAB" Interactive

	Add Map Auto Layer funafuti

	Set Map Window MapWinID Zoom 0.6 Units "km" Center (179.1994, -8.518482)

'	++++++++++++++++ import Access Table +++++++++++++++++++++++++++++++++++++++++
	
	Register Table PathAccess + DatabaseName   
	Type ACCESS Table "TMP_RoofTank" 
	Into PathVectorL + "TMP_RoofTank.TAB"

	Open Table PathVectorL + "TMP_RoofTank.TAB" Interactive

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++ Link MapInfo and Access Table +++++++++++++++++++++++++++++++

	Select * from Roof_MI, TMP_RoofTank 
	where Roof_MI.Roof_ID = TMP_RoofTank.Roof_ID 'And TMP_RoofTank.StoragePerson <> 0 
	into LinkTable

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Add to current mapper +++++++++++++++++++++++++++++++
	
	set map redraw off
	Add Map Layer LinkTable
	set map redraw on

'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Create thematic Map +++++++++++++++++++++++++++++++

   shade window MapWinID LinkTable with StoragePerson ranges apply all use color Brush (2,45144,16777215)  
	100: 1100 Brush (2,45144,16777215) Pen (1,2,0) ,
	1100: 1700 Brush (2,5296272,16777215) Pen (1,2,0) ,
	1700: 2600 Brush (2,11591872,16777215) Pen (1,2,0) ,
	2600: 2700 Brush (2,16752800,16777215) Pen (1,2,0) ,
	2700: 18500 Brush (2,16728128,16777215) Pen (1,2,0) 
	default Brush (2,16777215,16777215) Pen (1,2,0)  '# use 1 round 100 inflect on Brush (2,16777215,16777215) at 3 by 0 color 1 # 
 
'	++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'	++++++++++++++++++++++++ Create Map Legend +++++++++++++++++++++++++++++++

  	set legend window MapWinID layer prev 
		display on 
		shades on 
		symbols off 
		lines off 
		count on 
		title auto Font ("Arial",0,9,0) 
		subtitle "House without ID -> no survey" Font ("Arial",0,8,0) 
		ascending off ranges Font ("Arial",0,8,0) 
		auto display off ,
		auto display on ,
		auto display on ,
		auto display on ,
		auto display on ,
		auto display on  
 
	Set Window WIN_LEGEND 
		Position (0.5,3) Units "cm"
		Width 6 Units "cm"
		Height 4.2 Units "cm"

	Open Window Legend

	Run Menu Command M_QUERY_UNSELECT

	set map redraw off
	Set Map Layer LinkTable Label Font ("Arial",257,12,0,16777215) Overlap On
	Set Map Layer LinkTable Label Auto On 
   	Set Map Layer Roof_MI Display Global Global Pen (2,2,0)  Global Brush (6,16711680,16777215)
	set map redraw on

		Res_Main:
			Exit Sub

		Err_Manage:
			note "Error: " + str$(Err()) + ", " + Error$()
			Resume Res_Main

	End sub